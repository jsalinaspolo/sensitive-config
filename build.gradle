buildscript {
  repositories {
    mavenLocal()
    jcenter()
    maven { url 'https://dl.bintray.com/jspcore/maven' }
  }

  dependencies {
  }
}

plugins {
  id "com.jfrog.bintray" version "1.7.3"
}

group 'com.jspcore'
version '0.0.1-SNAPSHOT'
description = 'A library to parse configuration with sensitive values.'

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
  jcenter()
}

ext{
  jacksonVersion = '2.9.3'
}

dependencies {
  compile "com.bettercloud:vault-java-driver:3.0.0"

  compile "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion"
  compile "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
  compile "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
  compile "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jacksonVersion"

  compile "org.slf4j:slf4j-api:1.7.22"
  compile "ch.qos.logback:logback-classic:1.1.8"

  testCompile "junit:junit:4.12"
  testCompile "org.assertj:assertj-core:3.6.1"
  testCompile "org.mockito:mockito-core:1.10.19"

  testCompile "org.testcontainers:testcontainers:1.4.3"
  testCompile "org.bouncycastle:bcprov-jdk15on:1.58"
  testCompile "org.bouncycastle:bcpkix-jdk15on:1.58"
}

apply plugin: "jacoco"
jacocoTestReport {
  reports {
    xml.enabled true
    html.enabled true
  }
}

def pomConfig = {
  name project.name
  description project.description
  url "http://www.jspcore.com/"

  licenses {
    license {
      name "MIT License"
      url "https://opensource.org/licenses/MIT"
      distribution "repo"
    }
  }
  scm {
    connection "scm:https://mustaine@github.com/mustaine/sensitive-config.git"
    developerConnection "scm:git@github.com:mustaine/sensitive-config.git"
    url "https://github.com/mustaine/sensitive-config"
  }
  developers {
    developer {
      id "jspcore"
      name "Team JSPCore"
    }
  }
}

task javadocJar(type: Jar) {
  classifier = 'javadoc'
  from javadoc
}

task sourceJar(type: Jar) {
  from sourceSets.main.allSource
}

configurations {
  unitTestCompile.extendsFrom testCompile
  integrationTestCompile.extendsFrom testCompile
}

sourceSets {
  unitTest {
    compileClasspath += main.output + test.output
    runtimeClasspath += main.output + test.output
    java.srcDir file('src/test/java')
  }
  integrationTest {
    compileClasspath += main.output + test.output
    runtimeClasspath += main.output + test.output
    java.srcDir file('src/test-integration/java')
    resources.srcDirs += file('src/test-integration/resources')
  }
}

task unitTest(type: Test) {
  testClassesDirs = sourceSets.unitTest.output.classesDirs
  classpath = sourceSets.unitTest.runtimeClasspath
  testLogging {
    events "passed", "skipped", "failed"
  }
}

task integrationTest(type: Test) {
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  testLogging {
    events "passed", "skipped", "failed"
  }
}

//check.dependsOn integrationTest
integrationTest.mustRunAfter test

publishing {
  publications {
    MyPublication(MavenPublication) {
      from components.java
      groupId 'com.jspcore'
      artifactId 'sensitive-config'
      version '0.0.1'

      artifacts {
        artifact sourceJar {
          classifier "sources"
        }

        artifact javadocJar {
          classifier "javadoc"
        }
      }

      pom.withXml {
        def root = asNode()
        asNode().appendNode('description', project.description)
        root.children().last() + pomConfig
      }
    }
  }
}

bintray {
//  dryRun = true
  user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
  key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

  publications = ['MyPublication']
  pkg {
    repo = 'maven'
    name = 'sensitive-config'
    userOrg = 'jspcore'
    licenses = ['MIT']
    vcsUrl = 'https://github.com/mustaine/sensitive-config.git'
    version {
      name = '0.0.1'
      vcsTag = '0.0.1'
      released = new Date()
    }
  }
}



